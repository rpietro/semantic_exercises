PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX dc: <http://purl.org/dc/elements/1.1/>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX ns: <http://diseasesdiagnosis.com/vocab#>

SELECT ?diseaseName

WHERE
{

    ?patient rdf:type foaf:Person .
    ?patient foaf:name ?patientName .

    ?patient ns:hasSymptom ?optionalSymptom .
    ?optionalSymptom dc:subject ?meshOptionalSymptom .
    ?meshOptionalSymptom rdf:value ?optionalSymptomID .
    ?meshOptionalSymptom rdfs:label ?optionalSymptomName .

    ?disease rdf:type ?diseaseType .
    ?diseaseType dc:subject ?meshDiseaseType .
    ?meshDiseaseType rdf:value "C23.550.288" .
    ?meshDiseaseType rdfs:label "Disease" .

    ?disease dc:subject ?meshDisease .
    ?meshDisease rdfs:label ?diseaseName .

    ?disease ns:minimumQuantityOfOptionalSymptoms ?quantityOfOptionalSymptoms .

    ?disease ns:optionalSymptoms ?optionalSymptom .
    ?optionalSymptom dc:subject ?meshOptionalSymptom .
    ?meshOptionalSymptom rdf:value ?optionalSymptomID .
    ?meshOptionalSymptom rdfs:label ?optionalSymptomName .


    FILTER NOT EXISTS {

        ?disease ns:mandatorySymptoms ?mandatorySymptom .
        ?mandatorySymptom dc:subject ?meshMandatorySymptom .
        ?meshMandatorySymptom rdf:value ?mandatorySymptomID .
        ?meshMandatorySymptom rdfs:label ?mandatorySymptomName .

        FILTER NOT EXISTS {

            ?patient ns:hasSymptom ?mandatorySymptom .
            ?mandatorySymptom dc:subject ?meshMandatorySymptom .
            ?meshMandatorySymptom rdf:value ?mandatorySymptomID .
            ?meshMandatorySymptom rdfs:label ?mandatorySymptomName .

        }
    }

}

GROUP BY ?diseaseName ?quantityOfOptionalSymptoms HAVING ( COUNT(?optionalSymptom) >= ?quantityOfOptionalSymptoms )
ORDER BY COUNT(?optionalSymptom)